tasks.register('copySamples', Copy) {
    def outputsDir = layout.buildDirectory.dir('samples')
    into(outputsDir)

    from('src/main/java') {
        into 'src/main/java'
    }
    from('src/main/resources') {
        into 'src/main/resources'
    }
    from('input') {
        into 'input'
    }
    from("$rootProject.projectDir/gradle") {
        into 'gradle'
    }
    from("$rootProject.projectDir") {
        include 'gradlew*'
    }
    from('run-samples.gradle')

    doLast {
        def targetDir = outputsDir.get().asFile
        file("$targetDir/build.gradle").text = file('build.gradle').getText()
                .replaceAll(".*copy-samples.gradle.*", "version = '$rootProject.version'")
                .replaceAll("(?s)(dependencies) \\{.*?}", '$1 {\n' +
                        "    implementation fileTree(dir: '../lib', include: '*.jar')\n" +
                        "    annotationProcessor fileTree(dir: '../lib', include: '*.jar')\n}")

        file("$targetDir/settings.gradle").text = "plugins {\n" +
                "    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0'\n" +
                "}\n" +
                "rootProject.name = 'citygml4j-samples'"
    }
}